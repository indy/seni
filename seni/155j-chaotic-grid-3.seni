(render-background)
(translate vector: (v2 (/ canvas/width 2) (/ canvas/height 2)))
(rotate angle: {5 (scalar max: 115)})
(scale scalar: {1.2 (scalar min: 0.1 max: 3.0)})
(render-main)

(define 
  num-squares-to-render {15 (int min: 2 max: 20)}
  gap-size {30 (int min: 5 max: 50)}
  num-squares (+ 2 num-squares-to-render)
  num-gaps (+ num-squares 1)
  square-size (/ (- canvas/width (* gap-size num-gaps)) num-squares)
  base-colour (col/rgb r: {1.0 (scalar)}
                       g: {0.0 (scalar)}
                       b: {0.3 (scalar)}
                       alpha: 1.0)
  colour-volatility {20 (int min: 2 max: 40)}
  focal-point (focal/point position: (v2 {500 (int max: canvas/width)}
                                         {500 (int max: canvas/height)})
                           distance: {500 (int max: canvas/width)})
  focal-power {50 (scalar max: 200)})

(fn (render-background colour: (col/rgb r: 1.0 g: 1.0 b: 0.9))
    (rect x: (/ canvas/width 2)
          y: (/ canvas/height 2)
          width: canvas/width
          height: canvas/height
          colour: colour))

(fn (map-to-position at: 0)
    (- (+ (* (+ gap-size square-size) at) (/ square-size 2) gap-size)
       (/ canvas/width 2)))

(fn (render-main)
    (loop (y from: 1 to:  (- num-squares 1))
      (loop (x from: 1 to:  (- num-squares 1))
        (define 
          p (v2 (map-to-position at: x)
                (map-to-position at: y))
          volatility: (* (focal-point position: p) focal-power))

        (stroked-bezier-rect position: p
                             width: square-size
                             height: square-size
                             seed: (+ x (* y num-squares))
                             colour: base-colour
                             colour-volatility: colour-volatility
                             volatility: volatility
                             iterations: {10 (int min: 1 max: 50)}
                             tessellation: 1
                             overlap: {0 (int max: 10)}
                             stroke-noise: 0))))
